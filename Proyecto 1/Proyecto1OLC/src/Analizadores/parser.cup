package Analizadores;
import java_cup.runtime.Symbol;
import Errores.*;
import Instrucciones.*;


parser code 
{:
    /*Method that is called when parser can be recovered*/
    public void syntax_error(Symbol s){ 
        //System.out.println("Unexpected token: " +s.value);
        //System.out.println("Error sintactico en la linea " + (s.right + 1) + " column " + (s.left + 1));
        Instruction.list.addError(new Error_("Unexpected token: " + s.value, "Sintactico", s.right + 1, s.left + 1));
    }

    /*Method that is called when parser can't be recovered*/
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        //System.out.println("Error sintactico unrecovered: " + s.value);
        //System.out.println("Error sintactico unrecovered en la linea " + s.right + " column " + s.left);
        Instruction.list.addError(new Error_("Error sintactico unrecovered: " + s.value, "Sintactico", s.right + 1, s.left + 1));
    } 
:}


terminal String tnum;
terminal String tletras;
terminal tpcoma, tunder;
terminal tinicio, tfin;
terminal tint, tstring, tbool, tchar;
terminal trest,tsum,tdiv,tmul,tpot,tmod, tparA, tparC, tcorA, tcorC;
terminal tmayor, tmenor, tmayor_igual, tmenor_igual, tes_igual, tdifer;
terminal tor, tand, tnot;
terminal tingresar, tcomo, tcon_val, tcoma, tvar_name, tarrow;
terminal tif, tthen, tendif, telse, telseif;
terminal tcadena, tboolean, tcaracter;
terminal tsegun, tdo, tfin_segun, tQn_A, tQn_C;
terminal tfor, thasta, tfin_for, tincrease;
terminal twhile, tend_while;
terminal trepeat, tuntil;
terminal treturn;
terminal tmetodo, tfin_metodo, tparams;
terminal tfuncion, tfin_funcion;
terminal texec, tprint, tprintln;

non terminal S;
non terminal INSTRUCTIONS, INSTRUCTION;
non terminal STATEMENT, VAR, DATATYPE;
non terminal ASSIGNMENT;
non terminal EXPRESSION;
non terminal CONDITIONAL, CONDITIONALS, CONDITION, REL, OP_REL;
non terminal SWITCH, VALOR, CASE, DEFAULT, C, OPC;
non terminal FOR, INCREASE, INSTRUCCIONES;
non terminal WHILE;
non terminal REPETIR;
non terminal RETURN;
non terminal METODO, PARAMS, LISTPARAMS;
non terminal FUNCION;
non terminal EXEC, PARAMETROS;
non terminal PRINT;
non terminal INIT;

precedence left tcoma;

precedence left tmayor, tmenor, tmayor_igual, tmenor_igual;
precedence left tes_igual, tdifer;

precedence left tand, tor;
precedence right tnot;
precedence left tsum, trest;
precedence left tdiv, tmul;
precedence left tmod, tpot;
precedence left tparA, tparC;
precedence left tcorA, tcorC;



start with S;

S ::= INIT
;

INIT ::= tinicio INSTRUCTIONS tfin
    | tinicio tfin
    | error
;

INSTRUCTIONS ::= INSTRUCTIONS INSTRUCTION
    | INSTRUCTION
;

INSTRUCTION ::= STATEMENT tpcoma
    | STATEMENT:a {: Instruction.list.addError(new Error_("Falta un: \";\"", "Sintactico", aright, -1)); :}
    | ASSIGNMENT tpcoma
    | ASSIGNMENT {: Instruction.list.addError(new Error_("Falta un: \";\"", "Sintactico", aright, -1)); :}
    | CONDITIONAL tendif
    | SWITCH tfin_segun
    | FOR tfin_for
    | WHILE tend_while
    | REPETIR
    | METODO tfin_metodo
    | FUNCION tfin_funcion
    | EXEC tpcoma
    | PRINT tpcoma
    | error tpcoma
    | error tendif
    | error tfin_segun
    | error tfin_for
    | error tend_while
    | error tfin_metodo
    | error tfin_funcion
;

STATEMENT ::= tingresar VAR tcomo DATATYPE tcon_val REL
;

VAR ::= VAR tcoma VAR
    | tvar_name
;

DATATYPE ::= tint
    | tstring
    | tbool
    | tchar
;

ASSIGNMENT ::= VAR tarrow REL;


CONDITIONAL ::= tif CONDITION tthen INSTRUCTIONS
    | tif CONDITION INSTRUCTIONS telse INSTRUCTIONS
    | tif CONDITION CONDITIONALS
;

CONDITIONALS ::= CONDITIONALS telseif CONDITION tthen INSTRUCTIONS
    | telse INSTRUCTIONS
;

CONDITION ::= CONDITION tor CONDITION
    | CONDITION tand CONDITION
    | tnot CONDITION
    | tparA CONDITION tparC
    | REL
;

REL ::= EXPRESSION OP_REL EXPRESSION
    | EXPRESSION
;

OP_REL ::= tmayor
    | tmenor
    | tmayor_igual
    | tmenor_igual
    | tes_igual
    | tdifer
;

EXPRESSION ::= trest EXPRESSION
    | EXPRESSION tsum EXPRESSION 
    | EXPRESSION trest EXPRESSION
    | EXPRESSION tdiv EXPRESSION 
    | EXPRESSION tmul EXPRESSION 
    | EXPRESSION tpot tcorA EXPRESSION tcorC 
    | EXPRESSION tmod EXPRESSION
    | tparA EXPRESSION tparC
    | tvar_name
    | tnum
    | tcadena
    | tboolean
    | tcaracter        
;

SWITCH ::= tsegun EXPRESSION tdo CASE DEFAULT;

CASE ::= CASE C
    | C
;

C ::= tQn_A OPC tQn_C tthen INSTRUCTIONS;

OPC ::= tnum
    | tcaracter
    | tcadena
;

DEFAULT ::= telse tthen INSTRUCTIONS
    | //empty
;

FOR ::= tfor tvar_name tarrow VALOR thasta VALOR INCREASE tdo INSTRUCTIONS
    | tfor tvar_name tarrow VALOR thasta VALOR INCREASE INSTRUCTIONS
;

INCREASE ::= tincrease EXPRESSION
    | //empty
;

INSTRUCCIONES ::= INSTRUCTIONS
    | //empty
;

WHILE ::= twhile CONDITION tdo INSTRUCCIONES;

REPETIR ::= trepeat INSTRUCCIONES tuntil CONDITION;

RETURN ::= treturn CONDITION tpcoma;

METODO ::= tmetodo tvar_name PARAMS INSTRUCTIONS;

PARAMS ::= tparams tparA LISTPARAMS tparC
    | //empty
;

LISTPARAMS ::= LISTPARAMS tcoma LISTPARAMS
    | tvar_name DATATYPE
;

FUNCION ::= tfuncion tvar_name DATATYPE PARAMS INSTRUCTIONS RETURN;

EXEC ::= texec tvar_name tparA PARAMETROS tparC;

PARAMETROS ::= PARAMETROS tcoma PARAMETROS 
    | EXPRESSION
    | //empty
;

PRINT ::= tprint REL
    | tprint EXEC
    | tprintln REL
    | tprintln EXEC
;